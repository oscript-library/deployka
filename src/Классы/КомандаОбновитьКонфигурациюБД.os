///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

#Использовать v8find

Перем Лог;

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт
	
    ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, "Обновление конфигурации базы данных");
    
    Парсер.ДобавитьПозиционныйПараметрКоманды(ОписаниеКоманды, "СтрокаПодключения", "Строка подключения к рабочему контуру");
    Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-db-user",
    	"Пользователь информационной базы");

    Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-db-pwd",
    	"Пароль пользователя информационной базы");

    Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-v8version",
    	"Маска версии платформы 1С");
	
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-v8bitness",
		СтрШаблон("Маска разрядности платформы 1С {%1|%2|%3|%4}. Требуется указать -v8version", 
			РазрядностьПлатформы.x86x64,
			РазрядностьПлатформы.x64x86,
			РазрядностьПлатформы.x86,
			РазрядностьПлатформы.x64));
		
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-uccode",
    	"Ключ разрешения запуска");

	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, 
    	"-allow-warnings",
    	"Разрешить предупреждения");
	
	Парсер.ДобавитьПараметрФлагКоманды(ОписаниеКоманды, 
		"-dynamic",
		"Разрешить динамическое обновление");
	
	Парсер.ДобавитьКоманду(ОписаниеКоманды);

КонецПроцедуры

Функция ВыполнитьКоманду(Знач ПараметрыКоманды) Экспорт
    
	ВозможныйРезультат = МенеджерКомандПриложения.РезультатыКоманд();
	
    СтрокаПодключения = ПараметрыКоманды["СтрокаПодключения"];
	Пользователь      = ПараметрыКоманды["-db-user"];
	Пароль            = ПараметрыКоманды["-db-pwd"];
	ПредупрежденияВозможны = ПараметрыКоманды["-allow-warnings"];
	ИспользуемаяВерсияПлатформы = ПараметрыКоманды["-v8version"];
	ИспользуемаРазрядностьПлатформы = ПараметрыКоманды["-v8bitness"];
	КлючРазрешенияЗапуска = ПараметрыКоманды["-uccode"];
	ДинамическоеОбновление = ПараметрыКоманды["-dynamic"];
	
	Если ПустаяСтрока(СтрокаПодключения) Тогда
		Лог.Ошибка("Не задана строка подключения");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;
	
	Конфигуратор = ЗапускПриложений.НастроитьКонфигуратор(
		СтрокаПодключения,
		Пользователь,
		Пароль,
		ИспользуемаяВерсияПлатформы,
		ИспользуемаРазрядностьПлатформы);
	
	Если Не ПустаяСтрока(КлючРазрешенияЗапуска) Тогда
		Конфигуратор.УстановитьКлючРазрешенияЗапуска(КлючРазрешенияЗапуска);
	КонецЕсли;
	
	Лог.Информация("Запускаю обновление конфигурации БД");
	Попытка
		Конфигуратор.ОбновитьКонфигурациюБазыДанных(Не ПредупрежденияВозможны,,ДинамическоеОбновление);
		Текст = Конфигуратор.ВыводКоманды();
		Если Не ПустаяСтрока(Текст) Тогда
			Лог.Информация(Текст);
		КонецЕсли;
	Исключение
		Лог.Ошибка(Конфигуратор.ВыводКоманды());
		Возврат ВозможныйРезультат.ОшибкаВремениВыполнения;
	КонецПопытки;
	
	Возврат ВозможныйРезультат.Успех;
    
КонецФункции

Лог = Логирование.ПолучитьЛог("vanessa.app.deployka");