
///////////////////////////////////////////////////////////////////////////////////////////////////
// Прикладной интерфейс

Перем Лог;

Процедура ЗарегистрироватьКоманду(Знач ИмяКоманды, Знач Парсер) Экспорт
	
    ОписаниеКоманды = Парсер.ОписаниеКоманды(ИмяКоманды, "Формирование поставки конфигурации");
    
    Парсер.ДобавитьПозиционныйПараметрКоманды(ОписаниеКоманды, "СтрокаПодключения", "Строка подключения к рабочему контуру");
    Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-db-user",
    	"Пользователь информационной базы");

    Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-db-pwd",
    	"Пароль пользователя информационной базы");

    Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-v8version",
    	"Маска версии платформы 1С");
	
	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-uccode",
    	"Ключ разрешения запуска");

	Парсер.ДобавитьИменованныйПараметрКоманды(ОписаниеКоманды, 
    	"-release-folder",
    	"Каталог формирования поставки");
	
	Парсер.ДобавитьКоманду(ОписаниеКоманды);

КонецПроцедуры

Функция ВыполнитьКоманду(Знач ПараметрыКоманды) Экспорт
    
	ВозможныйРезультат = МенеджерКомандПриложения.РезультатыКоманд();
	
    СтрокаПодключения = ПараметрыКоманды["СтрокаПодключения"];
	Пользователь      = ПараметрыКоманды["-db-user"];
	Пароль            = ПараметрыКоманды["-db-pwd"];
	КаталогПоставки	  = ПараметрыКоманды["-release-folder"];
	ИспользуемаяВерсияПлатформы = ПараметрыКоманды["-v8version"];
	КлючРазрешенияЗапуска = ПараметрыКоманды["-uccode"];
	
	Если ПустаяСтрока(СтрокаПодключения) Тогда
		Лог.Ошибка("Не задана строка подключения");
		Возврат ВозможныйРезультат.НеверныеПараметры;
	КонецЕсли;
	
	Конфигуратор = ЗапускПриложений.НастроитьКонфигуратор(
		СтрокаПодключения,
		Пользователь,
		Пароль,
		ИспользуемаяВерсияПлатформы);
	
	Если Не ПустаяСтрока(КлючРазрешенияЗапуска) Тогда
		Конфигуратор.УстановитьКлючРазрешенияЗапуска(КлючРазрешенияЗапуска);
	КонецЕсли;
	
	Лог.Информация("Каталог поставки: " + КаталогПоставки);
	Если Прав(КаталогПоставки, 1) = "\" ИЛИ Прав(КаталогПоставки, 1) = "\" Тогда
	Иначе
		КаталогПоставки = "" + КаталогПоставки + "\";
	КонецЕсли;
	
	ФайлПоставки = СтрШаблон("%1%2\1Cv8.cf", КаталогПоставки, Формат(ТекущаяДата(), "ДФ='гггг-ММ-дд-ЧЧмм'"));
	Лог.Информация("Файл поставки: " + ФайлПоставки);
	
	Лог.Информация("Запускаю формирование поставки конфигурации");
	Попытка
		Конфигуратор.СоздатьФайлыПоставки(ФайлПоставки);
		Текст = Конфигуратор.ВыводКоманды();
		Если Не ПустаяСтрока(Текст) Тогда
			Лог.Информация(Текст);
		КонецЕсли;
	Исключение
		Лог.Ошибка(Конфигуратор.ВыводКоманды());
		Возврат ВозможныйРезультат.ОшибкаВремениВыполнения;
	КонецПопытки;
	
	Возврат ВозможныйРезультат.Успех;
    
КонецФункции

Лог = Логирование.ПолучитьЛог("vanessa.app.deployka");